buildscript {
	repositories { jcenter() }
}

plugins {
	id "nebula.project" version "3.3.0"
	id "nebula.source-jar" version "4.9.1"
	id "nebula.javadoc-jar" version "4.9.1"
	id "nebula.maven-publish" version "4.9.1"
	id "nebula.nebula-release" version "4.0.1"
	id 'nebula.nebula-bintray' version '3.3.4'
}

apply plugin: 'java'

group = 'de.gessnerfl.logback'
description = """Logback :: GSON :: Formatter"""

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories { jcenter() }

dependencies {
	compile group: 'ch.qos.logback.contrib', name: 'logback-json-core', version:'0.1.3'
	compile group: 'com.google.code.gson', name: 'gson', version:'2.7'
	testCompile group: 'com.google.guava', name: 'guava', version:'19.0'
	testCompile group: 'org.mockito', name: 'mockito-core', version:'1.10.19'
	testCompile group: 'junit', name: 'junit', version:'4.12'
	testCompile group: 'org.hamcrest', name: 'hamcrest-junit', version:'2.0.0.0'
}

publishing {
	publications {
		BintrayPublication(MavenPublication) {
			from components.java
			groupId project.group
			artifactId project.name
			version project.version
		}
	}
}

bintray {
	user = project.hasProperty('bintrayUser') ? project.getProperty('bintrayUser') : null
	key = project.hasProperty('bintrayKey') ? project.getProperty('bintrayKey') : null

	publications = ['BintrayPublication']
	dryRun = false
	publish = true
	pkg {
		repo = 'oss-de.gessnerfl'

		name = user
		desc = project.description
		userOrg = null

		websiteUrl = 'https://github.com/gessnerfl/logback-gson-formatter'
		issueTrackerUrl = 'https://github.com/gessnerfl/logback-gson-formatter/issues'
		vcsUrl = 'https://github.com/gessnerfl/logback-gson-formatter.git'
		licenses = ['Apache-2.0']
		labels = [
			'logback',
			'gson',
			'formatter'
		]
		publicDownloadNumbers = true
		attributes = [:]
		version {
			name = project.version
			vcsTag = 'v'+project.version
			attributes = [:]
			gpg {
				sign = true
				passphrase = project.hasProperty('gpgPassphrase') ? project.getProperty('gpgPassphrase') : null
			}
			mavenCentralSync {
				sync = true
				user = project.hasProperty('sonatypeUsername') ? project.getProperty('sonatypeUsername') : null
				password = project.hasProperty('sonatypePassword') ? project.getProperty('sonatypePassword') : null
			}
		}
	}
}
