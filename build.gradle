buildscript {
	repositories { jcenter() }
}

plugins {
	id "com.cinnober.gradle.semver-git" version "2.2.2"
	id "com.jfrog.bintray" version "1.7.1"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group = 'de.gessnerfl.logback'
description = """Logback :: GSON :: Formatter"""

ext.scmUrl = 'git@github.com:gessnerfl/logback-gson-formatter.git'
ext.projectUrl = 'https://github.com/gessnerfl/logback-gson-formatter'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories { jcenter() }

dependencies {
	compile group: 'ch.qos.logback.contrib', name: 'logback-json-core', version:'0.1.3'
	compile group: 'com.google.code.gson', name: 'gson', version:'2.7'
	testCompile group: 'com.google.guava', name: 'guava', version:'19.0'
	testCompile group: 'org.mockito', name: 'mockito-core', version:'1.10.19'
	testCompile group: 'junit', name: 'junit', version:'4.12'
	testCompile group: 'org.hamcrest', name: 'hamcrest-junit', version:'2.0.0.0'
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

publishing {
	publications {
		BintrayPublication(MavenPublication) {
			from components.java
            artifact sourcesJar
            artifact javadocJar
			
			groupId project.group
			artifactId project.name
			version project.version
			
			pom.withXml {
				asNode().children().last() + {
					resolveStrategy = Closure.DELEGATE_FIRST

					name project.name
					description project.description
					url project.projectUrl

					scm {
						connection 'scm:git:'+project.scmUrl
						developerConnection 'scm:git:'+project.scmUrl
						url project.scmUrl
					}

					licenses {
						license {
							name 'The Apache License, Version 2.0'
							url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						}
					}

					developers {
						developer {
							id 'gessnerfl'
							name 'Florian Gessner'
							email 'flo.gessner@gmail.com'
						}
					}
				}
			}
		}
	}
}

bintray {
	user = project.hasProperty('bintrayUser') ? project.getProperty('bintrayUser') : null
	key = project.hasProperty('bintrayKey') ? project.getProperty('bintrayKey') : null
	
	publications = ['BintrayPublication']
	dryRun = false
	publish = true
	pkg {
		repo = 'oss-de.gessnerfl'

		name = project.name
		desc = project.description
		userOrg = null

		websiteUrl = project.projectUrl
		issueTrackerUrl = project.projectUrl+'/issues'
		vcsUrl = project.scmUrl
		licenses = ['Apache-2.0']
		labels = [
			'logback',
			'gson',
			'formatter'
		]
		publicDownloadNumbers = true
		attributes = [:]
		version {
			name = project.version
			vcsTag = project.version
			attributes = [:]
			gpg {
				sign = true
				passphrase = project.hasProperty('gpgPassphrase') ? project.getProperty('gpgPassphrase') : null
			}
			mavenCentralSync {
				sync = true
				user = project.hasProperty('sonatypeUsername') ? project.getProperty('sonatypeUsername') : null
				password = project.hasProperty('sonatypePassword') ? project.getProperty('sonatypePassword') : null
			}
		}
	}
}
